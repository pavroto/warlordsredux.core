#include "includes.inc"
private _reviveActionId = player addAction [
    "<t color='#00ff00'>Revive</t>",
    {
        _this spawn {
            params ["_target", "_caller", "_actionId", "_arguments"];

            // start revive
            private _reviveTarget = player getVariable ["WL2_reviveTarget", objNull];
            if (isNull _reviveTarget) exitWith {};

            [[0, 8, 2]] call WL2_fnc_actionLockCamera;

            [player, ["AinvPknlMstpSlayWrflDnon_medic"]] remoteExec ["switchMove", 0];
            private _reviveSuccess = false;
            private _startCheckingUnhold = false;
            private _endTime = serverTime + 5;
            while { true } do {
                // interrupts
                if (!alive player) then {
                    break;
                };
                if (lifeState player == "INCAPACITATED") then {
                    break;
                };
                if (!alive _reviveTarget) then {
                    break;
                };
                if (lifeState _reviveTarget != "INCAPACITATED") then {
                    break;
                };

                private _inputAction = inputAction "Action" + inputAction "ActionContext";
                if (_startCheckingUnhold && _inputAction > 0) then {
                    break;
                };
                if (_inputAction == 0) then {
                    _startCheckingUnhold = true;
                };

                if (serverTime >= _endTime) then {
                    _reviveSuccess = true;
                    break;
                };
                uiSleep 0.01;
            };

            private _settingsMap = profileNamespace getVariable ["WL2_settings", createHashMap];
            private _hitmarkerVolume = _settingsMap getOrDefault ["hitmarkerVolume", 0.5];
            if (_reviveSuccess) then {
                playSoundUI ["AddItemOk", _hitmarkerVolume * 2];

                if (!isNull _reviveTarget) then {
                    [_reviveTarget] remoteExec ["WL2_fnc_revive", _reviveTarget];

                    if (isPlayer [_reviveTarget]) then {
                        private _reviveRewardTimers = player getVariable ["WL_reviveRewardTimers", createHashMap];
                        private _unitTimer = _reviveRewardTimers getOrDefault [hashValue _reviveTarget, 0];
                        if (_unitTimer < serverTime) then {
                            [player, "revived"] remoteExec ["WL2_fnc_handleClientRequest", 2];
                            private _newTimer = serverTime + 300;
                            _reviveRewardTimers set [hashValue _reviveTarget, _newTimer];
                            player setVariable ["WL_reviveRewardTimers", _reviveRewardTimers];
                        };
                    };
                };
            } else {
                playSoundUI ["AddItemFailed", _hitmarkerVolume * 2];
            };

            player setVariable ["WL2_reviveTarget", objNull];
            [player, [""]] remoteExec ["switchMove", 0];
            cameraOn cameraEffect ["Terminate", "BACK"];
        };
    },
    [],
    50,
    false,
    false,
    "",
    "alive (player getVariable ['WL2_reviveTarget', objNull])",
    0,
    false,
    "",
    ""
];
player setVariable ["WL2_reviveActionId", _reviveActionId];

[
    player,
    "<t color='#00ff00'>Hold on longer</t>",
    "\A3\Ui_f\data\IGUI\Cfg\HoldActions\holdAction_revive_ca.paa",
    "\A3\Ui_f\data\IGUI\Cfg\HoldActions\holdAction_revive_ca.paa",
    "lifeState player == 'INCAPACITATED'",
    "lifeState player == 'INCAPACITATED'",
    {},
    {},
    {
        params ["_target", "_caller", "_actionId", "_arguments"];
        player setVariable ["WL2_downedLiveTime", 90];
        private _currentExpirationTime = player getVariable ["WL2_expirationTime", 0];
        _currentExpirationTime = _currentExpirationTime + 65;
        player setVariable ["WL2_expirationTime", _currentExpirationTime, true];
        player removeAction _actionId;
    },
    {},
    [],
    1,
    1,
    true,
    true,
    false
] call BIS_fnc_holdActionAdd;

player addAction [
	"Customization",
	{
        0 spawn WLC_fnc_buildMenu;
	},
	nil,
	1.5,
	true,
	true,
	"",
	"lifeState player == 'INCAPACITATED'",
	5,
	true,
	"",
	""
];

player setCaptive false;
player setVariable ["WL2_alreadyHandled", false, 2];
player setVariable ["WL_unconsciousTime", 0];
setPlayerRespawnTime (getMissionConfigValue ["respawnDelay", 30]);